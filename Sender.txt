#include <SPI.h>
#include <LoRa.h>

#define ROW1 2
#define ROW2 3
#define ROW3 4
#define ROW4 5

#define COL1 6
#define COL2 7
#define COL3 8
#define COL4 9

char keyMap[4][4] = {
  {'1', '2', '3', 'A'},
  {'4', '5', '6', 'B'},
  {'7', '8', '9', 'C'},
  {'*', '0', '#', 'D'}
};

int rowPins[4] = {COL4, COL3, COL2, COL1};
int colPins[4] = {ROW4, ROW3, ROW2, ROW1};

String enteredNumber = ""; // Stores the entered number sequence

void setup() {
  Serial.begin(9600);
  
  // Initialize LoRa module
  if (!LoRa.begin(433E6)) {
    Serial.println("LoRa initialization failed!");
    while (1);
  }
  Serial.println("LoRa Sender initialized.");
  
  // Initialize keypad
  for (int i = 0; i < 4; i++) {
    pinMode(rowPins[i], OUTPUT);
    digitalWrite(rowPins[i], HIGH);
    pinMode(colPins[i], INPUT_PULLUP);
  }
}

void loop() {
  char key = scanKeypad(); // Check for keypress
  
  if (key != '\0') {
    Serial.print("Key Pressed: ");
    Serial.println(key);
    
    if ((key >= '0' && key <= '9') || (key == 'A') || (key == 'B') || (key == 'C')) {
      // Add digit or letter to the entered number
      enteredNumber += key;
    } else if (key == '#') {
      // Send the entered number via LoRa
      LoRa.beginPacket();
      LoRa.print("E101*"+enteredNumber);
      LoRa.endPacket();

      Serial.print("Sent: ");
      Serial.println(enteredNumber);
      
      enteredNumber = ""; // Clear the entered number
    }
  }
}

// Function to scan the keypad
char scanKeypad() {
  for (int row = 0; row < 4; row++) {
    digitalWrite(rowPins[row], LOW);
    for (int col = 0; col < 4; col++) {
      if (digitalRead(colPins[col]) == LOW) {
        digitalWrite(rowPins[row], HIGH);
        delay(300); // Debounce delay
        return keyMap[row][col];
      }
    }
    digitalWrite(rowPins[row], HIGH);
  }
  return '\0';
}
